plugins {
    id 'org.xtext.builder' version "${xtextPluginVersion}"
    id 'java-library'
}

version = '1.0-SNAPSHOT'

dependencies {
    implementation "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
    implementation 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'

    xtextLanguages('org.eclipse.emf:org.eclipse.emf.ecore.xcore:+') {
        exclude group: 'org.antlr', module: 'antlr-runtime'
    }
}

sourceSets {
    main {
        java.srcDirs += "src-gen"
    }
}

xtext {
    version = "${xtextVersion}"
    languages {
        xcore {
            setup = 'org.eclipse.emf.ecore.xcore.XcoreStandaloneSetup'
            generator.outlet.producesJava = true
            generator.outlet.cleanAutomatically = true
        }
    }
    sourceSets {
        main {
            srcDir 'model'
            // Move the generated Xcore output to the src-gen directory
            // Also need to be set in the xcore file in the @GenModel annotation !
            output {
                dir(xtext.languages.xcore.generator.outlet, 'src-gen')
            }
        }
    }
}

clean {
   delete 'src-gen/'
}
